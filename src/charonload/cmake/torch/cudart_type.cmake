function(charonload_detect_cudart_type)
    if(DEFINED CACHE{CHARONLOAD_TORCH_CUDART_STATIC} OR DEFINED CACHE{CHARONLOAD_TORCH_CUDART_SHARED})
        return()
    endif()

    charonload_message(CHECK_START "Detecting cudart library type of Torch ...")

    set(CHARONLOAD_TORCH_CUDART_STATIC OFF CACHE STRING "Static cudart library linked to Torch")
    set(CHARONLOAD_TORCH_CUDART_SHARED OFF CACHE STRING "Shared cudart library linked to Torch")
    if(TARGET torch_cuda)
        get_target_property(TORCH_CUDA_DEPENDENCIES torch_cuda INTERFACE_LINK_LIBRARIES)

        get_target_property(DEPENDENCIES torch_cuda INTERFACE_LINK_LIBRARIES)
        if(DEPENDENCIES)
            while(DEPENDENCIES)
                list(POP_FRONT DEPENDENCIES lib)

                list(FIND PROCESSED_DEPENDENCIES ${lib} lib_processed)
                if(TARGET ${lib} AND lib_processed STREQUAL "-1")
                    if(lib STREQUAL "CUDA::cudart_static")
                        set(CHARONLOAD_TORCH_CUDART_STATIC ON CACHE STRING "Static cudart library linked to Torch" FORCE)
                    elseif(lib STREQUAL "CUDA::cudart")
                        set(CHARONLOAD_TORCH_CUDART_SHARED ON CACHE STRING "Shared cudart library linked to Torch" FORCE)
                    endif()

                    get_target_property(LIB_DEPENDENCIES ${lib} INTERFACE_LINK_LIBRARIES)
                    if(LIB_DEPENDENCIES)
                        list(APPEND DEPENDENCIES ${LIB_DEPENDENCIES})
                    endif()
                    get_target_property(LIB_DEPENDENCIES ${lib} LINK_LIBRARIES)
                    if(LIB_DEPENDENCIES)
                        list(APPEND DEPENDENCIES ${LIB_DEPENDENCIES})
                    endif()

                    list(APPEND PROCESSED_DEPENDENCIES ${lib})
                endif()
            endwhile()
        endif()
    endif()

    if(CHARONLOAD_TORCH_CUDART_STATIC AND CHARONLOAD_TORCH_CUDART_SHARED)
        message(WARNING "Detected presence of both static and shared cudart libraries. This may potentially result in unexpected linker errors.")
    endif()

    if(CHARONLOAD_TORCH_CUDART_STATIC OR CHARONLOAD_TORCH_CUDART_SHARED)
        charonload_message(CHECK_PASS "done (cudart: STATIC=${CHARONLOAD_TORCH_CUDART_STATIC}, SHARED=${CHARONLOAD_TORCH_CUDART_SHARED})")
    else()
        charonload_message(CHECK_FAIL "done")
    endif()
endfunction()
